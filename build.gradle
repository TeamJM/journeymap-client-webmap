buildscript {
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.20'
    }
}

plugins {
    id 'net.neoforged.moddev' version '0.1.112' apply false
}

configurations.all {
    // Check for snapshots more frequently than Gradle's default of 1 day. 0 = every build.
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'kotlin'


    sourceSets {
        main.kotlin.srcDirs = ['src/main/java', 'src/main/kotlin']
    }
    sourceSets.main.resources { srcDir 'src/generated/resources' }

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
    java.withSourcesJar()

    jar {
        dependsOn 'processDocs'
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        manifest {
            attributes([
                    "Specification-Title"     : project.name,
                    "Specification-Vendor"    : project.group_id,
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : project.group_id,
                    "Implementation-URL"      : "http://journeymap.info",
                    "Implementation-Timestamp": getDate()
            ])
        }
    }

    repositories {

        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = "JourneyMap (Public)"
            url = "https://jm.gserv.me/repository/maven-public/"
        }
        maven {
            url "https://www.cursemaven.com"
        }

        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        // used for local mods in the libs folder
        flatDir {
            dirs '../libs'
        }
    }

    dependencies {
        implementation group: 'info.journeymap', name: 'webmap-client', version: project.journeymap_webmap_version, changing: true
        compileOnly "curse.maven:journeymap-32274:${project.journeymap_forge_version}"
//        implementation "ignored:journeymap-forge:1.21.1-6.0.0-beta.27"
        // for annotations
        implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

        // external libs
//        implementation 'org.slf4j:slf4j-api:2.0.16'
        implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.9.20'
        implementation group: 'io.javalin', name: 'javalin', version: project.javalin_version
        testImplementation "curse.maven:journeymap-32274:${project.journeymap_forge_version}"
//        testImplementation "ignored:journeymap-forge:1.21.1-6.0.0-beta.27"

    }

    plugins.withType(JavaPlugin) {
        dependencies {
            implementation enforcedPlatform('org.slf4j:slf4j-api:2.0.9')
        }
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 21
    }

    compileKotlin {
        source(project(":Common").sourceSets.main.allSource)
        inputs.file "../gradle.properties"
        kotlinOptions {
            jvmTarget = "21"
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        }
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format(project.dateFormat)
    return formattedDate
}
